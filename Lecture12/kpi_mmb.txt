SVG Card (Satış vs Hedef) v3 = 
VAR UnitText    = "Satış"
VAR Actual      = [toplam_satis_adedi]
VAR GoalValue   = [toplam_hedef]
VAR Pct         = DIVIDE ( [toplam_satis_adedi], [toplam_hedef] )
VAR PctSafe     = COALESCE ( Pct, 0 )
VAR PctClamped  = MIN ( MAX ( PctSafe, 0 ), 1 )
VAR RAG =
    IF ( ISBLANK ( Pct ), "gray",
        IF ( Pct < 0.8, "#e62c29",
            IF ( Pct < 1, "#F4E40E", "#2ECC71" ) ) )

VAR BarX  = 24
VAR BarY  = 120
VAR BarW  = 200
VAR BarH  = 20
VAR FillW = ROUND ( PctClamped * BarW, 0 )

RETURN
"data:image/svg+xml;utf8," &
"<svg xmlns='http://www.w3.org/2000/svg' viewBox='0 0 300 180'>" &
"<rect x='10' y='10' rx='8' ry='8' width='280' height='160' fill='white' stroke='#D9D9D9' stroke-width='2'/>" &
"<text x='24' y='44' font-family='Segoe UI' font-size='18' fill='#333'>" & UnitText & "</text>" &
"<text x='24' y='90' font-family='Segoe UI' font-size='40' font-weight='700' fill='" & RAG & "'>" &
    IF ( ISBLANK ( Pct ), "-", FORMAT ( PctSafe, "0%" ) ) & "</text>" &
"<rect x='170' y='58' rx='4' ry='4' width='120' height='30' fill='lavender'/>" &
"<text x='178' y='78' font-family='Segoe UI' font-size='16' fill='#333'>Goal: " &
    IF ( ISBLANK ( GoalValue ), "-", FORMAT ( GoalValue, "#,0" ) ) & "</text>" &
"<defs><linearGradient id='bar' x1='0' y1='0' x2='1' y2='0'><stop offset='0' stop-color='#05a'/>" &
"<stop offset='1' stop-color='cyan'/></linearGradient></defs>" &
"<rect x='" & BarX & "' y='" & BarY & "' rx='12' ry='12' width='" & BarW & "' height='" & BarH & "' fill='#E2E2E2'/>" &
"<rect x='" & BarX & "' y='" & BarY & "' rx='12' ry='12' width='" & FillW & "' height='" & BarH & "' fill='url(#bar)'/>" &
"</svg>"
